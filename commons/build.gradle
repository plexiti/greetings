buildscript {

	ext {

		kotlinVersion = "1.1.3-2"
		springBootVersion = "1.5.3.RELEASE"
		gradleCucumberVersion = "0.9"

	}

	repositories {

		mavenCentral()

		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }

		maven { url "https://plugins.gradle.org/m2/" }

	}

	dependencies {

        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")
        classpath("com.h2database:h2:1.4.191")

	}

}

apply plugin: "kotlin"
apply plugin: "kotlin-spring"

group = "plexiti"
version = "0.0.1-SNAPSHOT"

sourceCompatibility = 1.8

compileKotlin {

	kotlinOptions.jvmTarget = "1.8"

}

compileTestKotlin {

	kotlinOptions.jvmTarget = "1.8"

}

// Add integration test source sets
sourceSets {
    integration { sourceSet ->
        ["java", "kotlin", "api"].each {
            if (!sourceSet.hasProperty(it)) return
            sourceSet."$it".srcDir file("src/integration/${it}")
        }
    }
}

// Setup dependencies for integration testing
dependencies {
    integrationCompile sourceSets.main.output
    integrationCompile sourceSets.test.output
    integrationCompile configurations.testCompile
    integrationRuntime configurations.testRuntime
}

// Define integration test task
task integration(type: Test, dependsOn: test) {
    testClassesDir = sourceSets.integration.output.classesDir
    classpath = sourceSets.integration.runtimeClasspath
}

// Make sure 'check' task calls integration test
check.dependsOn integration

dependencies {

	compile("org.springframework.boot:spring-boot-starter:${springBootVersion}")

    compile("org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}")

    compile("org.apache.camel:camel-spring-boot-starter:2.19.0")
    compile("org.apache.camel:camel-jpa:2.19.0")

    compile("com.fasterxml.jackson.module:jackson-module-kotlin:2.8.7")

    compile("org.springframework.boot:spring-boot-starter-amqp:${springBootVersion}")

	compile("org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlinVersion}")
	compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")

	testCompile("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
	testCompile("org.assertj:assertj-core:3.8.0")
    testRuntime("com.h2database:h2:1.4.191")

}

repositories {
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://repo.spring.io/libs-milestone" }
    mavenCentral()
}
