buildscript {

	ext {

		kotlinVersion = "1.1.3-2"
		springBootVersion = "1.5.3.RELEASE"
		gradleCucumberVersion = "0.9"

	}

	repositories {

		mavenCentral()

		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }

		maven { url "https://plugins.gradle.org/m2/" }

	}

	dependencies {

        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
        classpath("org.jetbrains.kotlin:kotlin-allopen:${kotlinVersion}")

		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.springframework:springloaded:1.2.6.RELEASE")

		classpath("com.github.samueltbrown:gradle-cucumber-plugin:${gradleCucumberVersion}")

		classpath("se.transmode.gradle:gradle-docker:1.2")

        classpath("gradle.plugin.com.boxfuse.client:flyway-release:4.2.0")
        classpath("com.h2database:h2:1.4.191")

	}

}

apply plugin: "idea"

apply plugin: "kotlin"
apply plugin: "kotlin-spring"

apply plugin: "com.github.samueltbrown.cucumber"

apply plugin: "docker"

apply plugin: "org.flywaydb.flyway"

apply plugin: "org.springframework.boot"

group = "plexiti"
version = "0.0.1-SNAPSHOT"

sourceCompatibility = 1.8

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}

compileKotlin {

	kotlinOptions.jvmTarget = "1.8"

}

compileTestKotlin {

	kotlinOptions.jvmTarget = "1.8"

}

dependencyManagement {
    imports {
        mavenBom 'org.camunda.bpm:camunda-bom:7.7.0'
    }
}

dependencies {

    compile project(':flows')

	compile("org.springframework.boot:spring-boot-starter")
	compile("org.springframework.boot:spring-boot-devtools")

    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-data-rest")

    runtime("org.flywaydb:flyway-core:4.2.0")
    runtime("org.postgresql:postgresql:42.1.1")
    runtime("com.h2database:h2:1.4.191")

    compile("org.apache.camel:camel-spring-boot-starter:2.19.0")
    compile("org.apache.camel:camel-jpa:2.19.0")

    compile("com.fasterxml.jackson.module:jackson-module-kotlin:2.8.7")

    compile("org.camunda.bpm.extension.springboot:camunda-bpm-spring-boot-starter:2.1.2")

    compile("org.camunda.bpm.extension.springboot:camunda-bpm-spring-boot-starter:2.1.2")

    compile("org.camunda.spin:camunda-spin-core")
    compile("org.camunda.spin:camunda-spin-dataformat-all")
    compile("org.camunda.bpm:camunda-engine-plugin-spin")

    compile("org.springframework.boot:spring-boot-starter-amqp")

	compile("org.springframework.boot:spring-boot-starter-web")
	compile("org.jetbrains.kotlin:kotlin-stdlib-jre8:${kotlinVersion}")
	compile("org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}")

	testCompile("org.springframework.boot:spring-boot-starter-test")
	testCompile("org.assertj:assertj-core:3.8.0")

	cucumberCompile("info.cukes:cucumber-java:1.2.5")
	cucumberCompile("info.cukes:cucumber-junit:1.2.5")
	cucumberCompile("info.cukes:cucumber-spring:1.2.5")

}

repositories {

    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://repo.spring.io/libs-milestone" }

    mavenCentral()

}

cucumber {

	formats = ["pretty", "json:build/cucumber/cucumber.json", "junit:build/cucumber/cucumber.xml", "html:build/cucumber/html"]

	tags = ["~@Ignore"]

}

docker {

    maintainer = "Martin Schimak <martin.schimak@plexiti.com>"

}

task buildDocker(type: Docker) {

    push = false
    dockerfile = file("Dockerfile")
    tagVersion="latest"
    doFirst {
        copy {
            from jar
            into stageDir
        }
    }

}

flyway {

    url = "jdbc:h2:file:./build/greetings"
    user = "sa"

}

assemble.finalizedBy(buildDocker)
check.dependsOn('cucumber')
